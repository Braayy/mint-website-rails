- set_meta_tags title: 'Guide / Reference / Components / Properties'

%h1 Properties

%p
  Properties can be defined for components with the
  %b property
  keyword:

%pre
  %code
    :escaped
      component Test {
        property size : String = "small"
        property color : String = "red"

        fun render : Html {
          <div>
            <{ color }>
            <{ size }>
          </div>
        }
      }

%ul
  %li
    The name of the property must start with a lowercase letter and can only contain letters and numbers.

  %li
    Properties are referenced by
    %b name
    within the component (in
    %b styles, functions, computed properties,
    etc...)

%p.hint.hint--warning
  = octicon "alert"

  %span
    Properties must be fully defined (meaning types in it cannot have any type variables).

%h2 Required properties

%p
  A property can be
  %b required
  by leaving out the default value:

%pre
  %code
    :escaped
      component User {
        property name : String

        fun render {
          <div>
            "User: \#{name}"
          </div>
        }
      }

%p
  This means when using this component, this property must be set. If it's not
  then the type checker will show you an error.

%h2 Optional type

%p
  The
  %b type annotation
  can be left out if a default value is set:

%pre
  %code
    :escaped
      component User {
        property name = "Joe"

        fun render {
          <div>
            "User: \#{name}"
          </div>
        }
      }

%h2 Passing properties

%p
  Property values are passed to the component when it is rendered:

%pre
  %code
    :escaped
      component Main {
        fun render : Html {
          <Test color="blue" size="big"/>
        }
      }

%p.hint.hint--info
  = octicon "info"
  %span
    All properties are type checked. Attempting to set an incompatible value will show an error.

%p
  There are some examples of passing different things:

%pre
  %code
    :escaped
      component Main {
        fun render : Html {
          <Test
            items=[item1, item2]
            color={variable}
            size="10px"/>
        }
      }

%h2 Children

%p
  The
  %code children
  property is special and for now it can only have the type
  %code Array(Html).

%p
  It is used to access the children which are passed to the component:

%pre
  %code
    :escaped
      component Container {
        property children : Array(Html) = []

        fun render {
          <div>
            <{ children }>
          </div>
        }
      }

      component Main {
        fun render {
          <Container>
            "I am a child node!"
          </Container>
        }
      }

%p.hint.hint--info
  = octicon "info"
  %span
    Currently the property must be defined, but in a future release it will be optional.
